# Copyright (c) 2020-2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/lite/config/component/lite_component.gni")
import("//build/lite/ndk/ndk.gni")

if (ohos_kernel_type != "liteos_m") {
  shared_library("player_lite_impl") {
    sources = [
      "binder/buffersource/src/buffer_source.cpp",
      "binder/player_control/decoder/decoder.cpp",
      "binder/player_control/player/fsm/src/fsm_common.cpp",
      "binder/player_control/player/fsm/src/hi_fsm.cpp",
      "binder/player_control/player/fsm/src/hi_state.cpp",
      "binder/player_control/player/fsm/src/hi_state_machine.cpp",
      "binder/player_control/player/fsm/src/hi_state_machine_observer.cpp",
      "binder/player_control/player/fsm/src/message_looper.cpp",
      "binder/player_control/player/fsm/src/mmessage_pool.cpp",
      "binder/player_control/player/liteplayer.cpp",
      "binder/player_control/player/liteplayer_comm.cpp",
      "binder/player_control/player/liteplayer_state_machine.cpp",
      "binder/player_control/sink/player_audio_sink.cpp",
      "binder/player_control/sink/player_sink_manager.cpp",
      "binder/player_control/sink/player_sync.cpp",
      "binder/player_control/sink/player_video_sink.cpp",
      "binder/player_control/source/player_demux.cpp",
      "binder/player_control/source/player_source.cpp",
      "binder/player_impl.cpp",
    ]
    cflags = [ "-fPIC" ]
    cflags += [ "-Wall" ]
    cflags_cc = cflags
    include_dirs = [
      "//third_party/bounds_checking_function/include",
      "//foundation/multimedia/media_lite/interfaces/innerkits",
      "//foundation/multimedia/utils/lite/hals",
      "//foundation/multimedia/utils/lite/interfaces/kits",
      "//drivers/peripheral/display/interfaces/include",
      "//drivers/peripheral/base",
    ]
    outdir = rebase_path("$root_out_dir")
    public_configs = [ ":player_impl_external_library_config" ]
    ldflags = [ "-L$outdir" ]
    ldflags += [ "-laudio_hw" ]
    ldflags += [ "-lcodec" ]
    ldflags += [ "-lformat_hw" ]
    ldflags += [ "-lhdi_videodisplayer" ]
    public_deps = [
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//foundation/graphic/surface:lite_surface",
      "//foundation/multimedia/utils/lite:media_common",
      "//third_party/bounds_checking_function:libsec_shared",
      "//third_party/iniparser:iniparser",
    ]
  }

  config("player_impl_external_library_config") {
    include_dirs =
        [ "//foundation/multimedia/media_lite/frameworks/player_lite/binder" ]
    include_dirs +=
        [ "//foundation/multimedia/media_lite/interfaces/kits/player_lite" ]
    include_dirs += [ "//foundation/multimedia/media_lite/frameworks/player_lite/binder/buffersource/include" ]
    include_dirs += [ "//foundation/multimedia/media_lite/frameworks/player_lite/binder/player_control/include" ]
    include_dirs += [ "//foundation/multimedia/media_lite/frameworks/player_lite/binder/player_control/player" ]
    include_dirs += [ "//foundation/multimedia/utils/lite/interfaces/kits" ]
    include_dirs += [ "//drivers/peripheral/format/interfaces/include" ]
    include_dirs += [ "//foundation/multimedia/media_lite/frameworks/player_lite/binder/player_control/player/fsm/src" ]
    include_dirs += [ "//foundation/multimedia/media_lite/frameworks/player_lite/binder/player_control/player/fsm/include" ]
    include_dirs += [ "//foundation/multimedia/media_lite/frameworks/player_lite/binder/player_control/source" ]
    include_dirs += [ "//foundation/multimedia/media_lite/frameworks/player_lite/binder/player_control/sink" ]
    include_dirs += [ "//drivers/peripheral/codec/interfaces/include" ]
    include_dirs += [ "//foundation/multimedia/media_lite/frameworks/player_lite/binder/player_control/decoder" ]
    include_dirs += [ "//drivers/peripheral/audio/interfaces/include" ]
  }

  shared_library("player_lite") {
    sources = [ "binder/player.cpp" ]
    cflags = [ "-fPIC" ]
    cflags += [ "-Wall" ]
    cflags_cc = cflags
    include_dirs = [
      "//base/security/permission/interfaces/kits/permission_lite",
      "//foundation/multimedia/media_lite/services/player_lite/client/include",
      "//foundation/multimedia/media_lite/frameworks/player_lite",
    ]
    outdir = rebase_path("$root_out_dir")
    public_configs = [ ":player_external_library_config" ]
    ldflags = [ "-L$outdir" ]
    public_deps = [
      "//base/security/permission/services/permission_lite/pms_client:pms_client",
      "//foundation/distributedschedule/samgr_lite/samgr:samgr",
      "//foundation/graphic/surface:lite_surface",
      "//foundation/multimedia/media_lite/services/player_lite:player_client",
      "//foundation/multimedia/utils/lite:media_common",
      "//third_party/bounds_checking_function:libsec_shared",
    ]
  }

  config("player_external_library_config") {
    include_dirs =
        [ "//foundation/multimedia/media_lite/interfaces/kits/player_lite" ]
    include_dirs += [ "//foundation/multimedia/utils/lite/interfaces/kits" ]
  }

  lite_component("media_lite") {
    features = [
      ":player_lite_impl",
      ":player_lite",
      "//foundation/multimedia/media_lite/interfaces/kits/player_lite/js/builtin:audio_lite_api",
    ]
  }
} else {
  static_library("player_lite") {
    sources = [ "passthrough/player.cpp" ]
    include_dirs =
        [ "//base/hiviewdfx/hilog_lite/interfaces/native/innerkits/hilog" ]
    public_deps = [
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_static",
      "//foundation/multimedia/histreamer:histreamer",
    ]
    public_configs = [ ":player_direct_external_library_config" ]
  }

  config("player_direct_external_library_config") {
    include_dirs = [
      "//foundation/multimedia/media_lite/interfaces/innerkits",
      "//foundation/multimedia/frameworks/player_lite",
      "//foundation/multimedia/utils/lite/interfaces/kits",
      "//foundation/multimedia/media_lite/interfaces/kits/player_lite",
      "//drivers/peripheral/format/interfaces/include",
    ]
  }
}
